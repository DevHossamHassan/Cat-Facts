apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.letsgotoperfection.cat_facts"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
   // Specifies two flavor dimensions.
    flavorDimensions "mock"
    //consider using flavor dimensions.
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "mock"
        }
        prod {

        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.constraintLayoutVersion"
    implementation "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"
    //custom seekBar view
    implementation "com.xw.repo:bubbleseekbar:$rootProject.ext.bubbleseekbarLibraryVersion"
    //butterKnife
    implementation "com.jakewharton:butterknife:$rootProject.ext.butterknifeLibraryVersion"
    //Retrofit
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitLibraryVersion"
    implementation "com.google.code.gson:gson:$rootProject.ext.gsonLibraryVersion"
    //RxJava and RxAndroid
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxAndroidLibraryVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxJavaLibraryVersion"
    //Dagger
    implementation "com.google.dagger:dagger:$rootProject.ext.daggerLibraryVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.daggerLibraryVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.ext.daggerLibraryVersion"

    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknifeLibraryVersion"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitLibraryVersion"
    testImplementation "org.mockito:mockito-all:$rootProject.ext.mockitoLibraryVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestLibraryVersion"
    testImplementation "org.powermock:powermock-module-junit4:$rootProject.ext.powerMockitoLibraryVersion"
    testImplementation "org.powermock:powermock-api-mockito:$rootProject.ext.powerMockitoLibraryVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.runnerLibraryVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoCoreLibraryVersion"
}
